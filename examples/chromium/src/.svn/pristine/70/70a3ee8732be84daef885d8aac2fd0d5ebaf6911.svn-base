#######################################################################
# SConscript for xlib winsys

Import('*')

if env['platform'] != 'linux':
    Return()

env = env.Clone()

env.Tool('x11')

env.Prepend(LIBS = [
    ws_xlib,
    trace,
    rbug,
    identity,
#    gallium,
])

env.Append(CPPPATH = [
    '#src/gallium/drivers',
    '#src/gallium/include/state_tracker',
])


sources = [
    'graw_xlib.c',
    'graw_util.c',
]

env.Tool('x11')

if True:
    env.Append(CPPDEFINES = 'GALLIUM_SOFTPIPE')
    env.Prepend(LIBS = [softpipe])

if env['llvm']:
    env.Append(CPPDEFINES = 'GALLIUM_LLVMPIPE')
    env.Tool('udis86')
    env.Prepend(LIBS = [llvmpipe])

# Need this for trace, identity drivers referenced by
# gallium_wrap_screen().
#
env.Prepend(LIBS = [gallium])

# TODO: write a wrapper function http://www.scons.org/wiki/WrapperFunctions
graw = env.SharedLibrary(
    target ='graw',
    source = sources,
)

env.InstallSharedLibrary(graw, version=(1, 0))

graw = env.FindIxes(graw, 'SHLIBPREFIX', 'SHLIBSUFFIX')

Export('graw')
