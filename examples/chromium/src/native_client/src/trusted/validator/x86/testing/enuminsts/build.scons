# -*- python -*-
# Copyright (c) 2012 The Native Client Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import os
import sys
Import('env')

# vdiff is x86 only
if not env.Bit('target_x86'): Return()

# ------------------------------------------------------
# General adjustments to the environment for builds.

# Create environment for command-line tools and testing, rather than
# part of the TCB. Then define compile-time flag that communicates
# that we are compiling in the test environment (rather than for the TCB).
test_env = env.Clone()
test_env.Append(CPPDEFINES=['NACL_TRUSTED_BUT_NOT_TCB', 'NACL_RAGEL_DECODER'])

vdiff = test_env.ComponentProgram(
  'vdiff',
  ['vdiff.c', 'nacl_tester.c', 'ragel_tester.c', 'str_utils.c', 'text2hex.c'],
  EXTRA_LIBS=[test_env.NaClTargetArchSuffix('ncdis_util'),
              test_env.NaClTargetArchSuffix('ncvalidate'),
              test_env.NaClTargetArchSuffix('ncval_reg_sfi'),
              test_env.NaClTargetArchSuffix('nc_decoder'),
              test_env.NaClTargetArchSuffix('nc_opcode_modeling_verbose'),
              test_env.NaClTargetArchSuffix('nc_opcode_modeling'),
              test_env.NaClTargetArchSuffix('ncdis_decode_tables'),
              test_env.NaClTargetArchSuffix('ncval_base_verbose'),
              test_env.NaClTargetArchSuffix('ncval_base'),
              'validators',
              env.NaClTargetArchSuffix('ncvalidate'),
              env.NaClTargetArchSuffix('dfa_validate_caller'),
              'platform', 'gio', 'utils',
             ])

# NOTE: can't use stdout_golden here because the test runs too long,
# so the bots kill it because with this option there is no output.
if env.Bit('target_x86_64'):
  node = env.CommandTest(
    'vdiff.out', command=[vdiff, '--easydiff'],
    capture_output=0, time_warning=2500, time_error=5000)
  env.AddNodeToTestSuite(node, ['validator_diff_tests'], 'run_vdiff_test')

if env.Bit('target_x86_32'):
  # No golden output for 32-bit until more diffs are fixed
  node = env.CommandTest('vdiff.out', command=[vdiff],
                         time_warning=2500, time_error=5000)
  env.AddNodeToTestSuite(node, ['validator_diff_tests'], 'run_vdiff_test')
